This is applicable if only the source txt table is language dependent or has langu/spras field
Pls. check other file to see non language dependent one !!!!

First, in the source data cds view, we need to use this annotation  @ObjectModel.text.element: [<field_name_of_the_txt>], example
@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Value help for Request Status'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZTJ_VH_REQSTAT
  as select from ztj_reqstat_txt
{
      @ObjectModel.text.element: ['Stads']
  key staid as StaID,
      //@Semantics.language: true
  key spras as Spras,
      //@Semantics.text: true
      stads as Stads
}
where
  spras = 'E'

2nd, in the "Interface CDS view", we need to associate the main entity to the txt cds view like 
define root view entity ZTJ_DM_REQUEST
  as select from ztj_request as Request
    association [1..*] to ZTJ_VH_REQSTAT as _Stat on $projection.StaID = _Stat.StaID

complete code as below:
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: '##GENERATED Request RAP Generated'
define root view entity ZTJ_DM_REQUEST
  as select from ztj_request as Request
    association [1..*] to ZTJ_VH_REQSTAT as _Stat on $projection.StaID = _Stat.StaID
{
  key reqid as ReqID,
  key prjid as PrjID,
  key sprid as SprID,
  reqds as Reqds,
  reqor as Reqor,
  reqdt as Reqdt,
  resid as ResID,
  reqcd as Reqcd,
  plstdt as Plstdt,
  plfndt as Plfndt,
  acstdt as Acstdt,
  acfndt as Acfndt,
  @Consumption.valueHelpDefinition: [{entity: { name: 'ZTJ_VH_REQSTAT', 
                                                      element: 'StaID'  }}]  
  //@ObjectModel.text.association: '_Stat'  
  staid as StaID,
  @Semantics.user.createdBy: true
  crtby as Crtby,
  @Semantics.systemDate.createdAt: true
  crtdt as Crtdt,
  @Semantics.systemTime.createdAt: true
  crttm as Crttm,
  @Semantics.systemDateTime.createdAt: true
  crtst as Crtst,
  @Semantics.user.lastChangedBy: true
  chgby as Chgby,
  @Semantics.systemDate.createdAt: true
  chgdt as Chgdt,
  @Semantics.systemTime.createdAt: true
  chgtm as Chgtm,
  @Semantics.systemDateTime.lastChangedAt: true
  chgst as Chgst,
  
  _Stat
  
}

3rd, in the "projection cds view", we need to add annotation  @ObjectModel.text.element: [<field_name_of_the_txt>], a
add the association name into this projection view, and add the field_name_of_the_text field into this projection view with addition ": localized",
complete sample as below
Note: localized will make the field not appearing in its own field in the screen, instead it will be appeared in the code field...coolll!!!
@AccessControl.authorizationCheck: #NOT_REQUIRED
@Metadata.allowExtensions: true
@EndUserText.label: 'Projection View for ZTJ_DM_REQUEST'
@ObjectModel.semanticKey: [ 'ReqID', 'PrjID', 'SprID' ]
define root view entity ZTJ_P_REQUEST
  provider contract transactional_query
  as projection on ZTJ_DM_REQUEST
{
  key ReqID,
  key PrjID,
  key SprID,
  Reqds,
  Reqor,
  Reqdt,
  ResID,
  Reqcd,
  Plstdt,
  Plfndt,
  Acstdt,
  Acfndt,
  @ObjectModel.text.element: ['Stads'] //<=
  StaID,
  _Stat.Stads : localized,  // <=
  Crttm,
  Crtst,
  Chgtm,
  Chgst,
  
  _Stat
  
}


